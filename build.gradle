plugins {
    id 'com.adarshr.test-logger' version '2.1.0'
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'jacoco'
}

group = 'br.com.iot'
// encoding
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

ext {
    set('springCloudVersion', "Hoxton.SR7")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "Hoxton.SR7")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'

    implementation platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'

    implementation 'org.immutables:value:2.8.8'
    annotationProcessor 'org.immutables:value:2.8.8'
    implementation 'org.mapstruct:mapstruct:1.4.0.Beta3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.0.Beta3'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.projectreactor:reactor-test'

    implementation 'com.github.javafaker:javafaker:1.0.2'

}

test {
    useJUnitPlatform()
}

testlogger {
    theme 'mocha-parallel'
    showExceptions true
    showStackTraces true
    showFullStackTraces false
    showCauses true
    slowThreshold 2000
    showSummary true
    showSimpleNames false
    showPassed true
    showSkipped true
    showFailed true
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
    logLevel 'lifecycle'
}

wrapper {
    gradleVersion = '6.5.1'
    distributionType = Wrapper.DistributionType.ALL
}

bootJar {
    layered()
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            'br/com/iot/producer/simulator/api/ApplicationStarter.class',
                            'br/com/iot/producer/simulator/api/exception/**',
                            'br/com/iot/producer/simulator/api/mapper/**',
                            'br/com/iot/producer/simulator/api/config/**',
                            'br/com/iot/producer/simulator/api/model/**',
                            'br/com/iot/producer/simulator/api/controller/request/**',
                            'br/com/iot/producer/simulator/api/controller/response/**'
                    ]
            )
        }))
    }
}

check.dependsOn jacocoTestReport